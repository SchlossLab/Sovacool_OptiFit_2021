""" Download the reference databases and process with mothur """

wildcard_constraints:
    subset="bact_v4|bact_full",
    ref="silva|rdp|gg"

rule targets:
    input:
        expand("results/{ref}/{ref}.{region}.filter.unique.precluster.opti_mcc.0.03.cons.taxonomy", ref=("silva", "gg", "rdp"), region=("bact_v4", "bact_full"))

rule download_silva:
    output:
        tar=temp("data/silva/Silva.seed_v132.tgz"),
        fasta="data/silva/silva.fasta",
        tax="data/silva/silva.tax"
    params:
        fasta="data/silva/silva.seed_v132.align",
        tax="data/silva/silva.seed_v132.tax"
    benchmark:
        "benchmarks/silva/download.txt"
    shell:
        """
        wget -N -P data/silva/ http://www.mothur.org/w/images/7/71/Silva.seed_v132.tgz
        tar xzvf {output.tar} -C data/silva/
        mv {params.fasta} {output.fasta}
        mv {params.tax} {output.tax}
        """

rule download_rdp:
    output:
        tar=temp("data/rdp/Trainset16_022016.pds.tgz"),
        fasta="data/rdp/rdp.fasta",
        tax="data/rdp/rdp.tax"
    params:
        fasta="data/rdp/trainset16_022016.pds/trainset16_022016.pds.fasta",
        tax="data/rdp/trainset16_022016.pds/trainset16_022016.pds.tax"
    benchmark:
        "benchmarks/rdp/download.txt"
    shell:
        """
        wget -N -P data/rdp/ https://mothur.org/w/images/c/c3/Trainset16_022016.pds.tgz
        tar xvzf {output.tar} -C data/rdp/
        mv {params.fasta} {output.fasta}
        mv {params.tax} {output.tax}
        rm -rf data/rdp/trainset16_022016.pds/
        """

rule download_gg:
    output:
        fasta="data/gg/gg.fasta",
        tax="data/gg/gg.tax",
        tar_tax=temp("data/gg/Gg_13_8_99.taxonomy.tgz"),
        tar_ref=temp("data/gg/Gg_13_8_99.refalign.tgz")
    params:
        fasta="data/gg/gg_13_8_99.fasta",
        tax="data/gg/gg_13_8_99.gg.tax"
    benchmark:
        "benchmarks/gg/download.txt"
    shell:
        """
        wget -N -P data/gg/ http://www.mothur.org/w/images/6/68/Gg_13_8_99.taxonomy.tgz
        wget -N -P data/gg/ http://www.mothur.org/w/images/1/19/Gg_13_8_99.refalign.tgz
        tar -xzvf {output.tar_tax} -C data/gg/
        tar -xzvf {output.tar_ref} -C data/gg/
        mv {params.fasta} {output.fasta}
        mv {params.tax} {output.tax}
        """

rule get_bacteria:
    input:
        fasta="data/{ref}/{ref}.fasta",
        tax="data/{ref}/{ref}.tax"
    output:
        fasta="data/{ref}/{ref}.bacteria.fasta",
        tax="data/{ref}/{ref}.bacteria.tax"
    params:
        fasta="data/{ref}/{ref}.pick.fasta",
        tax="data/{ref}/{ref}.pick.tax"
    log:
        "log/{ref}/get_bacteria.log"
    benchmark:
        "benchmarks/{ref}/get_bacteria.txt"
    shell:
        """
        mothur '#set.logfile(name={log}); set.dir(input=data/{wildcards.ref}/);

        get.lineage(fasta={input.fasta}, taxonomy={input.tax}, taxon=Bacteria);
        summary.seqs(fasta=current);

        rename.file(input={params.fasta}, new={output.fasta});
        rename.file(input={params.tax}, new={output.tax}) '
        """

rule align:
    input:
        fasta=rules.get_bacteria.output.fasta,
        silva=rules.download_silva.output.fasta
    output:
        align="data/{ref}/{ref}.bacteria.align"
    wildcard_constraints:
        ref="rdp|gg"
    resources:
        procs=8
    log:
        "log/{ref}/align.log"
    benchmark:
        "benchmarks/{ref}/align.txt"
    shell:
        """
        mothur '#set.logfile(name={log}); set.dir(input=data/{wildcards.ref}/);
        align.seqs(candidate={input.fasta}, template={input.silva}, processors={resources.procs}); '
        """

rule rename_silva_bact:  # needed b/c silva doesn't get aligned to itself
    input:
        "data/silva/silva.bacteria.fasta"
    output:
        align="data/silva/silva.bacteria.align"
    shell:
        "cp {input} {output}"

rule subset_regions:
    input:
        align="data/{ref}/{ref}.bacteria.align" # can't use rules.align.output b/c silva is the alignment template
    output:
        sum="data/{ref}/{ref}.bacteria.summary",
        fasta_full="data/{ref}/{ref}.bact_full.fasta",
        fasta_v4="data/{ref}/{ref}.bact_v4.fasta"
    params:
        screen="data/{ref}/{ref}.bacteria.good.pcr.align"
    resources:
        procs=8
    log:
        "log/{ref}/subset_regions.log"
    benchmark:
        "benchmarks/{ref}/subset_regions.txt"
    shell:
        """
        mothur '#set.logfile(name={log}); set.dir(input=data/{wildcards.ref}/);
        summary.seqs(fasta={input.align}, processors={resources.procs});

        screen.seqs(fasta={input.align}, maxambig=0, maxhomop=8, start=11894, end=25319);
        summary.seqs();
        pcr.seqs(fasta=current, start=11894, end=25319, keepdots=F);
        summary.seqs();
        rename.file(input={params.screen}, new={output.fasta_v4});

        screen.seqs(fasta={input.align}, maxambig=0, maxhomop=8, start=1046, end=43116);
        summary.seqs();
        pcr.seqs(fasta=current, start=1046, end=43116, keepdots=F);
        summary.seqs();
        rename.file(input={params.screen}, new={output.fasta_full}) '
        """

rule filter:
    input:
        fasta="data/{ref}/{ref}.{region}.fasta",
        tax=rules.get_bacteria.output.tax
    output:
        filter="data/{ref}/{ref}.{region}.filter.fasta",
        names="data/{ref}/{ref}.{region}.filter.names",
        unique="data/{ref}/{ref}.{region}.filter.unique.fasta",
        preclust_fasta="data/{ref}/{ref}.{region}.filter.unique.precluster.fasta",
        preclust_count="data/{ref}/{ref}.{region}.filter.unique.precluster.count_table",
        preclust_accnos="data/{ref}/{ref}.{region}.filter.unique.precluster.accnos",
        preclust_tax="data/{ref}/{ref}.bacteria.{region}.precluster.tax"
    params:
        tax="data/{ref}/{ref}.bacteria.pick.tax"
    resources:
        procs=2
    log:
        "log/{ref}/filter.{region}.log"
    benchmark:
        "benchmarks/{ref}/filter.{region}.txt"
    shell:
        """
        mothur '#set.logfile(name={log}); set.dir(input=data/{wildcards.ref}/);

        filter.seqs(fasta={input.fasta}, trump=., vertical=T, processors={resources.procs});
        unique.seqs(fasta={output.filter});
        pre.cluster(fasta={output.unique}, name={output.names}, diffs=2, processors={resources.procs});
        list.seqs(fasta={output.preclust_fasta});
        get.seqs(taxonomy={input.tax}, accnos=current, name=current);

        summary.seqs(fasta=current, count=current);
        rename.file(input={params.tax}, new={output.preclust_tax}) '
        """

rule calc_dists:
    input:
        fasta=rules.filter.output.preclust_fasta
    output:
        dist="results/{ref}/{ref}.{region}.filter.unique.precluster.dist"
    log:
        "log/{ref}/calc_dists.{region}.log"
    benchmark:
        "benchmarks/{ref}/calc_dists.{region}.txt"
    resources:
        procs=12,
        walltime_hrs=48
    shell:
        """
        mothur '#set.logfile(name={log}); set.dir(input=data/{wildcards.ref}/, output=results/{wildcards.ref}/);
        dist.seqs(fasta={input.fasta}, cutoff=0.03, processors={resources.procs}) '
        """

rule cluster:
    input:
        dist=rules.calc_dists.output.dist,
        count=rules.filter.output.preclust_count
    output:
        list="results/{ref}/{ref}.{region}.filter.unique.precluster.opti_mcc.list",
        steps="results/{ref}/{ref}.{region}.filter.unique.precluster.opti_mcc.steps",
        sensspec="results/{ref}/{ref}.{region}.filter.unique.precluster.opti_mcc.sensspec"
    log:
        "log/{ref}/cluster.{region}.log"
    benchmark:
        "benchmarks/{ref}/cluster.{region}.txt"
    shell:
        """
        mothur '#set.logfile(name={log}); set.dir(input=data/{wildcards.ref}/, output=results/{wildcards.ref}/);
        cluster(column={input.dist}, count={input.count}, cutoff=0.03) '
        """

rule get_otu_reps:
    input:
        list=rules.cluster.output.list,
        count=rules.filter.output.preclust_count,
        tax=rules.filter.output.preclust_tax,
        dist=rules.calc_dists.output.dist
    output:
        tax="results/{ref}/{ref}.{region}.filter.unique.precluster.opti_mcc.0.03.cons.taxonomy",
        sum="results/{ref}/{ref}.{region}.filter.unique.precluster.opti_mcc.0.03.cons.tax.summary",
        names="results/{ref}/{ref}.{region}.filter.unique.precluster.opti_mcc.0.03.rep.names"
    log:
        "log/{ref}/get_otu_reps.{region}.log"
    benchmark:
        "benchmarks/{ref}/get_otu_reps.{region}.txt"
    shell:
        """
        mothur '#set.logfile(name={log}); set.dir(output=results/{wildcards.ref}/);
        make.shared(list={input.list}, count={input.count}, label=0.03)
        classify.otu(list={input.list}, count={input.count}taxonomy={input.tax}, label=0.03);
        get.oturep(column={input.dist}, list={input.list}, count={input.count}, method=abundance) '
        """
