""" Download the reference databases and process with mothur
"""

mothur = "mothur '#set.dir(input=data/{ref}/, output=data/{ref}/); set.logfile(name={log}) "

include: "code/silva.smk"
include: "code/rdp.smk"
include: "code/greengenes.smk"

wildcard_constraints:
    subset="bact_v4|bact_full",
    ref="silva|rdp|gg"

rule targets:
    input:
        expand("data/{ref}/{ref}.{subset}.filter.unique.precluster.opti_mcc.0.03.{ext}", ref=("silva", "gg", "rdp"), subset=("bact_v4", "bact_full"), ext=("cons.taxonomy", "cons.tax.summary", "rep.names"))

rule get_bacteria:
    input:
        fasta="data/{ref}/{ref}.fasta",
        tax="data/{ref}/{ref}.tax"
    output:
        fasta="data/{ref}/{ref}.bacteria.fasta",
        tax="data/{ref}/{ref}.bacteria.tax"
    log:
        "logfiles/{ref}/get_bact.log"
    params:
        fasta="data/{ref}/{ref}.pick.fasta",
        tax="data/{ref}/{ref}.pick.tax"
    shell:
        """
        {mothur}
        get.lineage(fasta={input.fasta}, tax={input.tax}, taxon=Bacteria) '
        mv {params.fasta} {output.fasta}
        mv {params.tax} {output.tax}
        """

rule align_to_silva:
    input:
        fasta=rules.get_bacteria.output.fasta,
        silva_ref=rules.download_silva.output.fasta
    output:
        align="data/{ref}/{ref}.bacteria.align"
    wildcard_constraints:
        ref="rdp|gg"
    shell:
        """
        {mothur}
        align.seqs(candidate={input.fasta}, template={input.silva_ref}) '
        """

rule subset_regions:
    input:
        align="data/{ref}/{ref}.bacteria.align"
    output:
        full_length="data/{ref}/{ref}.bact_full.fasta",
        v4="data/{ref}/{ref}.bact_v4.fasta"
    params:
        screen="data/{ref}/{ref}.bacteria.good.align",
        pcr="data/{ref}/{ref}.bacteria.pcr.fasta"
    shell:
        """
        {mothur}
        screen.seqs(fasta={input.align}, start=1046, end=43116)
        pcr.seqs(fasta={input.align}, start=11894, end=25319, keepdots=F) '
        mv {params.screen} {output.full_length}
        mv {params.pcr} {output.v4}
        """

rule filter:
    input:
        fasta="data/{ref}/{ref}.{subset}.fasta"
    output:
        filter="data/{ref}/{ref}.{subset}.filter.fasta",
        names="data/{ref}/{ref}.{subset}.filter.names",
        unique="data/{ref}/{ref}.{subset}.filter.unique.fasta",
        preclust_fasta="data/{ref}/{ref}.{subset}.filter.unique.precluster.fasta",
        preclust_names="data/{ref}/{ref}.{subset}.filter.unique.precluster.names"
    shell:
        """
        {mothur}
        filter.seqs(fasta={input.fasta}, trump=., vertical=T)
        unique.seqs(fasta={output.filter})
        pre.cluster(fasta={output.unique}, name={output.names}, diffs=2) '
        """

rule calc_dists:
    input:
        rules.filter.output.preclust_fasta
    output:
        "data/{ref}/{ref}.{subset}.filter.unique.precluster.dist"
    log:
        "logfiles/{ref}/calc_dists.{subset}.log"
    shell:
        """
        {mothur}
        dist.seqs(fasta={input}, cutoff=0.03) '
        """

rule cluster:
    input:
        dist=rules.calc_dists.output,
        names=rules.filter.output.preclust_names
    output:
        list="data/{ref}/{ref}.{subset}.filter.unique.precluster.opti_mcc.list",
        steps="data/{ref}/{ref}.{subset}.filter.unique.precluster.opti_mcc.steps",
        sensspec="data/{ref}/{ref}.{subset}.filter.unique.precluster.opti_mcc.sensspec"
    log:
        "logfiles/{ref}/cluster.{subset}.log"
    shell:
        """
        {mothur}
        cluster(column={input.dist}, name={input.names}, cutoff=0.03) '
        """

rule get_otu_reps:
    input:
        tax=rules.get_bacteria.output.tax,
        list=rules.cluster.output.list,
        names="data/{ref}/{ref}.{subset}.filter.unique.precluster.names"
    output:
        "data/{ref}/{ref}.{subset}.filter.unique.precluster.opti_mcc.0.03.cons.taxonomy",
        "data/{ref}/{ref}.{subset}.filter.unique.precluster.opti_mcc.0.03.cons.tax.summary",
        "data/{ref}/{ref}.{subset}.filter.unique.precluster.opti_mcc.0.03.rep.names"
    log:
        "logfiles/{ref}/otu_reps.{subset}.log"
    shell:
        """
        {mothur}
        classify.otu(taxonomy={input.tax}, list={input.list}, names={input.names})
        get.oturep(method=abundance, list={input.list}, name={input.names}) '
        """
