""" Benchmarking the OptiFit algorithm using an external reference database """
import shutil

# TODO: take a V4 data set and try it against a FL and V4 reference
wildcard_constraints:
    region="bact_v4|bact_full"

subworkflow prep_db:
    workdir:
        "../0_prep_db/"
subworkflow prep_samples:
    workdir:
        "../1_prep_samples/"

rule targets_fit_ref_db:
    input:
        expand('results/{dataset}/{ref}/{region}/method_{method}/printref_{printref}/seed_{seed}/{dataset}.optifit_mcc.sensspec', dataset=("mouse", ), ref=("silva",), region=("bact_v4",), method=("open",), printref=("f",), seed=range(1))

rule copy_preclust_inputs:  # because filter.seqs doesn't like hyphens in filepaths
    input:
        filter=prep_samples("data/{dataset}/{ref}/processed/{dataset}.filter"),
        fasta=prep_samples("data/{dataset}/{ref}/processed/{dataset}.fasta"),
        count=prep_samples("data/{dataset}/{ref}/processed/{dataset}.count_table"),
        column=prep_samples("results/{dataset}/{ref}/{dataset}.dist"),
        fasta_db=prep_db("data/{ref}/{ref}.{region}.fasta"),
        tax=prep_db("data/{ref}/{ref}.bacteria.tax")
    output:
        filter="data/{dataset}/{ref}/{region}/{dataset}.filter",
        fasta="data/{dataset}/{ref}/{region}/{dataset}.fasta",
        count="data/{dataset}/{ref}/{region}/{dataset}.count_table",
        column="data/{dataset}/{ref}/{region}/{dataset}.dist",
        fasta_db="data/{dataset}/{ref}/{region}/{ref}.{region}.fasta",
        tax="data/{dataset}/{ref}/{region}/{ref}.bacteria.tax"
    params:
        outdir="data/{dataset}/{ref}/{region}/"
    shell:
        """
        cp {input.filter} {output.filter}
        cp {input.fasta} {output.fasta}
        cp {input.count} {output.count}
        cp {input.column} {output.column}
        cp {input.fasta_db} {output.fasta_db}
        cp {input.tax} {output.tax}
        """

rule preclust_db:
    input:
        fasta_db=rules.copy_preclust_inputs.output.fasta_db,
        fasta_sample=rules.copy_preclust_inputs.output.fasta,
        tax=rules.copy_preclust_inputs.output.tax,
        hard_filter=rules.copy_preclust_inputs.output.filter
    output:
        fasta="data/{dataset}/{ref}/{region}/{ref}.{region}.filter.unique.precluster.fasta",
        count="data/{dataset}/{ref}/{region}/{ref}.{region}.filter.unique.precluster.pick.count_table",
        tax="data/{dataset}/{ref}/{region}/{ref}.{region}.pick.tax",
        dist="data/{dataset}/{ref}/{region}/{ref}.{region}.filter.unique.precluster.dist"
    params:
        tax="data/{dataset}/{ref}/{region}/{ref}.bacteria.pick.tax",
        outdir="data/{dataset}/{ref}/{region}/"
    resources:
        procs=8,
        walltime_hrs=48
    log:
        "log/{dataset}/{ref}/preclust_db.{region}.log"
    benchmark:
        "benchmarks/{dataset}/{ref}/preclust_db.{region}.txt"
    shell:
        """
        mothur '#set.logfile(name={log}); set.dir(output={params.outdir});

        filter.seqs(fasta={input.fasta_db}, hard={input.hard_filter}, processors={resources.procs});
        unique.seqs(fasta=current);
        pre.cluster(fasta=current, name=current, diffs=2, processors={resources.procs});
        dist.seqs(fasta=current, cutoff=0.03, processors={resources.procs});

        list.seqs(fasta=current);
        get.seqs(accnos=current, taxonomy={input.tax});
        get.seqs(accnos=current, count=current);
        summary.seqs(fasta=current, count=current);
        rename.file(input={params.tax}, new={output.tax}) '
        """

rule cluster_db:
    input:
        dist=rules.preclust_db.output.dist,
        count=rules.preclust_db.output.count
    output:
        list="results/{dataset}/{ref}/{region}/seed_{seed}/{ref}.{region}.filter.unique.precluster.opti_mcc.list",
        steps="results/{dataset}/{ref}/{region}/seed_{seed}/{ref}.{region}.filter.unique.precluster.opti_mcc.steps",
        sensspec="results/{dataset}/{ref}/{region}/seed_{seed}/{ref}.{region}.filter.unique.precluster.opti_mcc.sensspec",
    params:
        outdir="data/{dataset}/{ref}/{region}/seed_{seed}/"
    log:
        "log/{dataset}/{ref}/{region}/cluster.{region}.seed_{seed}.log"
    benchmark:
        "benchmarks/{dataset}/{ref}/{region}/cluster.{region}.seed_{seed}.txt"
    shell:
        """
        mothur '#set.logfile(name={log}); set.dir(output={params.outdir});
        set.seed(seed={wildcards.seed});
        cluster(column={input.dist}, count={input.count}, cutoff=0.03) '
        """

rule get_otu_reps_db:
    input:
        list=rules.cluster_db.output.list,
        count=rules.preclust_db.output.count,
        tax=rules.preclust_db.output.tax,
        dist=rules.preclust_db.output.dist
    output:
        tax="results/{dataset}/{ref}/{region}/{ref}.{region}.seed_{seed}.opti_mcc.0.03.cons.taxonomy",
        sum="results/{dataset}/{ref}/{region}/{ref}.{region}.seed_{seed}.opti_mcc.0.03.cons.tax.summary",
        count="results/{dataset}/{ref}/{region}/{ref}.{region}.seed_{seed}.opti_mcc.0.03.rep.count_table"
    params:
        outdir="results/{dataset}/{ref}/{region}/"
    log:
        "log/{dataset}/{ref}/{region}/get_otu_reps.{region}.seed_{seed}.log"
    benchmark:
        "benchmarks/{dataset}/{ref}/{region}/get_otu_reps.{region}.seed_{seed}.txt"
    shell:
        """
        mothur '#set.logfile(name={log}); set.dir(output={params.outdir});
        classify.otu(list={input.list}, count={input.count}, taxonomy={input.tax}, label=0.03);
        get.oturep(column={input.dist}, list={input.list}, count={input.count}, method=abundance) '
        """


rule fit_to_ref_db:
    input:
        reflist=rules.cluster_db.output.list,
        refcolumn=rules.preclust_db.output.dist,
        reffasta=rules.preclust_db.output.fasta,
        refcount=rules.preclust_db.output.count,
        fasta=rules.copy_preclust_inputs.output.fasta,
        count=rules.copy_preclust_inputs.output.count,
        column=rules.copy_preclust_inputs.output.column
    output:
        sensspec='results/{dataset}/{ref}/{region}/method_{method}/printref_{printref}/seed_{seed}/{dataset}.optifit_mcc.sensspec'
    params:
        outdir="results/{dataset}/{ref}/{region}/method_{method}/printref_{printref}/seed_{seed}/"
    benchmark:
        "benchmarks/{dataset}/{ref}/{region}/method_{method}/printref_{printref}/seed_{seed}/fit.txt"
    log:
        "log/{dataset}/{ref}/{region}/method_{method}/printref_{printref}/seed_{seed}/fit.log"
    shell:
        """
        mothur "#set.logfile(name={log}); set.dir(output={params.outdir});
        cluster.fit(reflist={input.reflist}, refcolumn={input.refcolumn}, reffasta={input.reffasta},refcount={input.refcount}, fasta={input.fasta}, count={input.count}, column={input.column}, printref={wildcards.printref}, method={wildcards.method}) "
        """
