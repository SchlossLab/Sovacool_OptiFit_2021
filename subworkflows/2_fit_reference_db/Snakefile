""" Benchmarking the OptiFit algorithm using an external reference database """
import shutil

# TODO: take a V4 data set and try it against a FL and V4 reference

subworkflow prep_db:
    workdir:
        "../0_prep_db/"
subworkflow prep_samples:
    workdir:
        "../1_prep_samples/"

rule fit_to_external_ref_db:
    input:
        expand('results/{dataset}/{ref}_{region}/method-{method}/printref-{printref}/seed-{seed}/{dataset}_vs_{ref}.optifit_mcc.sensspec', dataset=("soil",), ref=("silva", "gg", "rdp"), region=("bact_v4", "bact_full"), method=("open", "closed"), printref=("t", "f"), seed=range(10))

rule fit_to_ref:
    input:
        reflist=prep_db('results/{ref}/{ref}.{region}.seed_{seed}.opti_mcc.list'),
        refcolumn=prep_db("results/{ref}/{ref}.{region}.filter.unique.precluster.dist"),
        reffasta=prep_db("data/{ref}/{ref}.{region}.filter.unique.precluster.fasta"),
        fasta=prep_samples("data/{dataset}/processed/{dataset}.fasta"),
        count=prep_samples("data/{dataset}/processed/{dataset}.count_table"),
        column=prep_samples("results/{dataset}/{dataset}.dist")
    output:
        'results/{dataset}/{ref}_{region}/method-{method}/printref-{printref}/seed-{seed}/{dataset}_vs_{ref}.optifit_mcc.sensspec'
    params:
        output_dir="results/{dataset}/{ref}_{region}/method-{method}/printref-{printref}/seed-{seed}/"
    benchmark:
        "benchmarks/{dataset}/{ref}_{region}/method-{method}/printref-{printref}/seed-{seed}/fit.txt"
    log:
        "log/{dataset}/{ref}_{region}/method-{method}/printref-{printref}/seed-{seed}/fit.log"
    shell:
        """
        mothur "#set.logfile(name={log});
        set.dir(output={params.output_dir});
        cluster.fit(reflist={input.reflist}, refcolumn={input.refcolumn}, reffasta={input.reffasta}, fasta={input.fasta}, count={input.count}, column={input.column}, printref={wildcards.printref}, method={wildcards.method}) "
        """