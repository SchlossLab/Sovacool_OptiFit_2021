""" Benchmarking the OptiFit algorithm using subsets of datasets """

subworkflow prep_samples:
    workdir:
        "../1_prep_samples/"


datasets = ["human", "marine", "mouse", "soil"]
methods = ['open', 'closed']
printrefs=['f']
seeds = range(1, 11)
random_refweight_options = ['simple', 'abundance', 'distance']
ref_fracs = [i/10 for i in range(2,10,2)]
sample_fracs = [0.2]

wildcard_constraints:
    seed="\d+"

rule subset_targets:
    input:
        "results/sensspec.tsv",
        "results/benchmarks.tsv",
        'results/fraction_reads_mapped.tsv',
        expand('results/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/fit/method_{method}/printref_{printref}/{dataset}.pick.optifit_mcc.mod.sensspec',
            dataset=datasets, ref_weight=random_refweight_options, ref_frac=ref_fracs, sample_frac=sample_fracs, seed=seeds, method=methods, printref=printrefs),
        expand('results/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/fit/method_closed/printref_{printref}/fraction_reads_mapped.txt',
            dataset=datasets, ref_weight=random_refweight_options, ref_frac=ref_fracs, sample_frac=sample_fracs, seed=seeds, printref=printrefs)

rule split_weighted_subsample:
    input:
        code="../../code/py/split_weighted_subsample.py",
        fasta=prep_samples("data/{dataset}/processed/{dataset}.fasta"),
        count=prep_samples("data/{dataset}/processed/{dataset}.count_table"),
        dist=prep_samples("results/{dataset}/{dataset}.dist")
    output:
        ref_accnos="data/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/ref.accnos",
        sample_accnos="data/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/sample.accnos"
    benchmark:
        "benchmarks/{dataset}/split_weighted_subsample.refweight_{ref_weight}.reffrac_{ref_frac}.samplefrac_{sample_frac}.seed_{seed}.txt"
    script:
        "../../code/py/split_weighted_subsample.py"

rule prep_subsample_ref:
    input:
        accnos=rules.split_weighted_subsample.output.ref_accnos,
        fasta=prep_samples("data/{dataset}/processed/{dataset}.fasta"),
        count=prep_samples("data/{dataset}/processed/{dataset}.count_table"),
        dist=prep_samples("results/{dataset}/{dataset}.dist")
    output:
        fasta="data/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/ref/{dataset}.pick.fasta",
        count="data/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/ref/{dataset}.pick.count_table",
        dist="data/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/ref/{dataset}.pick.dist"
    log:
        "log/{dataset}/prep_subsample_ref.refweight_{ref_weight}.reffrac_{ref_frac}.samplefrac_{sample_frac}.seed_{seed}.txt"
    benchmark:
        "benchmarks/{dataset}/prep_subsample_ref.refweight_{ref_weight}.reffrac_{ref_frac}.samplefrac_{sample_frac}.seed_{seed}.txt"
    params:
        outdir="data/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/ref/"
    shell:
        """
        mothur '#set.logfile(name={log}); set.dir(output={params.outdir});
            get.seqs(accnos={input.accnos}, fasta={input.fasta});
            get.seqs(accnos=current, count={input.count});
            get.dists(accnos=current, column={input.dist}); '
        """

rule combine_for_fit:
    input:
        accnos_ref=rules.split_weighted_subsample.output.ref_accnos,
        accnos_sample=rules.split_weighted_subsample.output.sample_accnos,
        fasta=prep_samples("data/{dataset}/processed/{dataset}.fasta"),
        count=prep_samples("data/{dataset}/processed/{dataset}.count_table"),
        dist=prep_samples("results/{dataset}/{dataset}.dist")
    output:
        accnos_all="data/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/combined/{dataset}.accnos",
        fasta_all="data/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/combined/{dataset}.pick.fasta",
        count_all="data/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/combined/{dataset}.pick.count_table",
        dist_all="data/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/combined/{dataset}.pick.dist"
    params:
        outdir="data/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/combined/"
    log:
        "benchmarks/{dataset}/combine_for_fit.refweight_{ref_weight}.reffrac_{ref_frac}.samplefrac_{sample_frac}.seed_{seed}.txt"
    benchmark:
        "benchmarks/{dataset}/combine_for_fit.refweight_{ref_weight}.reffrac_{ref_frac}.samplefrac_{sample_frac}.seed_{seed}.txt"
    shell:
        """
        cat {input.accnos_ref} {input.accnos_sample} > {output.accnos_all}
        mothur '#set.logfile(name={log}); set.dir(output={params.outdir});
            get.seqs(accnos={output.accnos_all}, fasta={input.fasta});
            get.seqs(accnos=current, count={input.count});
            get.dists(accnos=current, column={input.dist}); '
        """

rule cluster_ref:
    input:
        dist=rules.prep_subsample_ref.output.dist,
        count=rules.prep_subsample_ref.output.count
    output:
        list="results/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/cluster/{dataset}.pick.opti_mcc.list",
        sensspec='results/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/cluster/{dataset}.pick.opti_mcc.sensspec'
    params:
        outdir="results/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/cluster/"
    log:
        "log/{dataset}/cluster.refweight_{ref_weight}.reffrac_{ref_frac}.samplefrac_{sample_frac}.seed_{seed}.log"
    benchmark:
        "benchmarks/{dataset}/cluster.refweight_{ref_weight}.reffrac_{ref_frac}.samplefrac_{sample_frac}.seed_{seed}.txt"
    resources:
        procs=8
    shell:
        """
        mothur '#set.logfile(name={log}); set.dir(output={params.outdir});
        set.seed(seed={wildcards.seed});
        set.current(processors={resources.procs});
        cluster(column={input.dist}, count={input.count}, cutoff=0.03) '
        """

rule fit_subsample:
    input:
        fasta=rules.combine_for_fit.output.fasta_all,
        count=rules.combine_for_fit.output.count_all,
        column=rules.combine_for_fit.output.dist_all,
        reflist=rules.cluster_ref.output.list
    output:
        sensspec='results/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/fit/method_{method}/printref_{printref}/{dataset}.pick.optifit_mcc.sensspec',
        list='results/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/fit/method_{method}/printref_{printref}/{dataset}.pick.optifit_mcc.list'
    params:
        outdir="results/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/fit/method_{method}/printref_{printref}/"
    log:
        "log/{dataset}/fit.method_{method}.printref_{printref}.refweight_{ref_weight}.reffrac_{ref_frac}.samplefrac_{sample_frac}.seed_{seed}.txt"
    benchmark:
        "benchmarks/{dataset}/fit.method_{method}.printref_{printref}.refweight_{ref_weight}.reffrac_{ref_frac}.samplefrac_{sample_frac}.seed_{seed}.txt"
    resources:
        procs=8
    shell:
        """
        mothur "#set.logfile(name={log}); set.dir(output={params.outdir});
        set.seed(seed={wildcards.seed});
        set.current(processors={resources.procs});
        cluster.fit(reflist={input.reflist}, fasta={input.fasta}, count={input.count}, column={input.column},  printref={wildcards.printref}, method={wildcards.method}) "
        """

rule reformat_opticlust_results3:  # to make compatible with optifit sensspec & plotting
    input:
        sensspec=rules.cluster_ref.output.sensspec,
        bench=rules.cluster_ref.benchmark,
        code="../../code/R/reformat_opticlust_results.R"
    output:
        sensspec='results/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/cluster/{dataset}.pick.opti_mcc.mod.sensspec',
        bench="benchmarks/{dataset}/cluster.refweight_{ref_weight}.reffrac_{ref_frac}.samplefrac_{sample_frac}.seed_{seed}.mod.txt"
    log:
        "log/{dataset}/reformat_opticlust_results.refweight_{ref_weight}.reffrac_{ref_frac}.samplefrac_{sample_frac}.seed_{seed}.txt"
    benchmark:
        "benchmarks/{dataset}/reformat_opticlust_results.refweight_{ref_weight}.reffrac_{ref_frac}.samplefrac_{sample_frac}.seed_{seed}.txt"
    params:
        dataset="{dataset}",
        seed="{seed}",
        ref_weight='{ref_weight}',
        ref_frac='{ref_frac}'
    script:
        "../../code/R/reformat_opticlust_results.R"

rule reformat_optifit_results3:  # for plotting
    input:
        sensspec=rules.fit_subsample.output.sensspec,
        bench=rules.fit_subsample.benchmark,
        code="../../code/R/reformat_optifit_results.R"
    output:
        sensspec='results/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/fit/method_{method}/printref_{printref}/{dataset}.pick.optifit_mcc.mod.sensspec',
        bench="benchmarks/{dataset}/fit.method_{method}.printref_{printref}.refweight_{ref_weight}.reffrac_{ref_frac}.samplefrac_{sample_frac}.seed_{seed}.mod.txt"
    log:
        "log/{dataset}/reformat_optifit_results.method_{method}.printref_{printref}.refweight_{ref_weight}.reffrac_{ref_frac}.samplefrac_{sample_frac}.seed_{seed}.txt"
    benchmark:
        "benchmarks/{dataset}/reformat_optifit_results.method_{method}.printref_{printref}.refweight_{ref_weight}.reffrac_{ref_frac}.samplefrac_{sample_frac}.seed_{seed}.txt"
    params:
        dataset="{dataset}",
        ref="NA",
        region="NA",
        seed="{seed}",
        printref='{printref}',
        method='{method}',
        ref_weight='{ref_weight}',
        ref_frac='{ref_frac}',
        sample_frac='{sample_frac}'
    script:
        "../../code/R/reformat_optifit_results.R"

rule merge_results3:
    input:
        sensspec=expand('results/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/fit/method_{method}/printref_{printref}/{dataset}.pick.optifit_mcc.mod.sensspec',
            dataset=datasets, ref_weight=random_refweight_options, ref_frac=ref_fracs, sample_frac=sample_fracs, seed=seeds, method=methods, printref=printrefs),
        bench=expand('benchmarks/{dataset}/fit.method_{method}.printref_{printref}.refweight_{ref_weight}.reffrac_{ref_frac}.samplefrac_{sample_frac}.seed_{seed}.mod.txt',
            dataset=datasets, method=methods, printref=printrefs, ref_weight=random_refweight_options, ref_frac=ref_fracs, sample_frac=sample_fracs, seed=seeds),
        code='../../code/R/merge_results.R'
    output:
        sensspec="results/sensspec.tsv",
        bench="results/benchmarks.tsv"
    benchmark:
        "benchmarks/merge_results.txt"
    script:
        '../../code/R/merge_results.R'

rule fraction_reads_mapped3:
    input:
        code="../../code/py/fraction_reads_mapped.py",
        list=rules.fit_subsample.output.list,
        count=rules.split_weighted_subsample.output.sample_accnos
    output:
        txt='results/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/fit/method_{method}/printref_{printref}/fraction_reads_mapped.txt'
    wildcard_constraints:
        method='closed'
    script:
        "../../code/py/fraction_reads_mapped.py"

rule merge_mapped_stats3:
    input:
        expand('results/{dataset}/refweight_{ref_weight}/reffrac_{ref_frac}/samplefrac_{sample_frac}/seed_{seed}/fit/method_closed/printref_{printref}/fraction_reads_mapped.txt',
            dataset=datasets, ref_weight=random_refweight_options, ref_frac=ref_fracs, sample_frac=sample_fracs, seed=seeds, printref=printrefs)
    output:
        txt='results/fraction_reads_mapped.tsv'
    shell:
        """
        echo "dataset\tseed\tmethod\tprintref\tfraction_mapped\tsample_frac\tref_frac\tref_weight\n" > {output.txt}
        cat {input} >> {output.txt}
        """
